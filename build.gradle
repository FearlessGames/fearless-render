lwjglVersion = '2.8.3'

coreLibDir = new File("fearless-render-core/lib")
twlLibDir = new File("fearless-render-twl/lib")

subprojects {
    apply plugin: 'java'
    group = "se.fearlessgames.fear"

    repositories {
        flatDir name: 'coreLibs', dirs: coreLibDir.absolutePath
        flatDir name: 'twlLibs', dirs: twlLibDir.absolutePath
        mavenCentral()
    }

    sourceCompatibility = 1.6

    defaultTasks 'build'

    test {
        maxParallelForks = 10
    }

    buildDir = "build"

    slf4jVersion = '1.6.4'

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.10'
        testCompile group: 'se.mockachino', name: 'mockachino', version: '0.6.0'
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
        runtime group: 'ch.qos.logback', name: 'logback-classic', version: "1.0.0"
    }
}

dependsOnChildren()

// Find any 3rd party libraries which have released new versions
// to the central Maven repo since we last upgraded.
task checkLibVersions << {
    def checked = [:]
    allprojects {
        configurations.each { configuration ->
            configuration.allDependencies.each { dependency ->
                def version = dependency.version
                if (!version.contains('SNAPSHOT') && !checked[dependency]) {
                    def group = dependency.group
                    def path = group.replace('.', '/')
                    def name = dependency.name
                    def url = "http://repo1.maven.org/maven2/$path/$name/maven-metadata.xml"
                    try {
                        def metadata = new XmlSlurper().parseText(url.toURL().text)
                        def newestVersion = metadata.versioning.latest
                        if(newestVersion == null || newestVersion.toString().isEmpty()) {
                            newestVersion = metadata.versioning.release
                        }
                        //println "Latest version of $name is $newestVersion"

                        if (!version.toString().equals(newestVersion.toString())) {
                            println "$group:$name $version -> $newestVersion"
                        }
                    } catch (FileNotFoundException e) {
                        logger.debug "Unable to download $url: $e.message"
                    } catch (org.xml.sax.SAXParseException e) {
                        logger.debug "Unable to parse $url: $e.message"
                    }
                    checked[dependency] = true
                }
            }
        }
    }
}



task wrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-8'
}